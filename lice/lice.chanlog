#
#   IRC Script Program. For use with ircii-EPIC5 clients.
#   Copyright (C) 2000 whitefang (thall@magenet.com) - Broken email.
#
# ---------------------------------------------------------------------------
# All code by whitefang, unless specified. Visit http://wolf.magenet.com
# ---------------------------------------------------------------------------
#
# Prior to LiCe 5.3 this file was taken from whitefang's wolf addon for LiCe4
# Since LiCe 5.3 we now use epic5's built in logging function.
#

IF (word(2 $loadinfo()) != [pf]) {
  LOAD -pf $word(1 $loadinfo());
  RETURN;
};

PACKAGE LiCe;

@ mkdir($lice.save_path/logs);

ALIAS _proc.startlog (ref, file, chn) {
  //^LOG $ref NAME $chn;
  //^LOG $ref FILENAME $file;
  //^LOG $ref ADD $chn;
  //^LOG $ref ON;
};   

ALIAS chanlog {
  UNLESS (@) {
    uecho Usage: /CHANLOG -a <channel>;
    uecho Usage: /CHANLOG -d <channel>;
    uecho Usage: /CHANLOG -l <channel>;
    RETURN;
  };
  @ :opts = [];
  WHILE (opts = getopt(optopt optarg "a:d:l::" $*)) {
    @ :chan = optarg;
    @ :file = [$lice.save_path/logs/$tolower($chan)\.log];
    @ :hash = hash_32bit($chan);
    SWITCH ($opts) {
      (a) {
        FE ($logctl(REFNUMS ALL)) logs {
          IF ((logctl(GET $logs NAME)) == [$chan]) {
          iecho Already logging $chan;
          RETURN;
          };
        };
        _proc.startlog $logctl(NEW) $file $chan;
        @lice.set(![$hash][C] $chan);
        iecho Chanlog: logging $chan to $file;
        CONTINUE;
      };
      (d) {
        @ rmlog = 0;
        FE ($logctl(REFNUMS ALL)) logs {
          IF ((logctl(GET $logs NAME)) == [$chan]) {
            @ rmlog = logs;
          };
        };
        UNLESS (rmlog) {
          iecho $chan isn't being logged, unable to delete;
          RETURN;
        };
        //^LOG $rmlog KILL;
        @lice.del(![$hash][C]);
        iecho Chanlog: no longer logging $chan;
      };
      (l) {
        //LOG LIST;
      };
    };
  };
};

#Hacked into LiCe before epic supported logging by tjbh/2000 - wolf script
#tjh/14
